Auteur: Jonathan Melly
Date: 24.09.2020
Entreprise: ETML
Description: CollecteurDePluie

Voici les ordres du programme CollecteurDePluie  :
	Masquer le curseur.
	Effacer l'écran.
	
    Afficher "+-------------------+\n".
	Afficher "|Collecteur de Pluie|\n".
	Afficher "+-------------------+".
	
	Afficher "\n\n*MENU*\n\n".
	
	Choisir la couleur rouge foncé pour le fond.
	Afficher "Jouer".
	Afficher "\n".
	Choisir la couleur noir pour le fond.
	Afficher "Quitter".
	
	//Gestion du MENU
	Créer une zone mémoire nommée #lignePremiereOption avec la valeur 6.
	Créer une zone mémoire nommée #ligneDerniereOption avec la valeur 7. 
	Créer une zone mémoire nommée #ligneOptionCourante avec #lignePremiereOption. 
	Créer une zone mémoire nommée #touchePressee avec le texte "vide" .
	
	Répéter tant que #touchePressee est différente de "Enter"
		
		Attendre la prochaine touche et la stocker dans #touchePressee.
		
		//L'option jouer est choisie et qu'on est pas déjà dessus
		Si #touchePressee est égale à "UpArrow" et #ligneOptionCourante est différente de #lignePremiereOption alors
			
			Placer le curseur à la ligne #lignePremiereOption.
			Placer le curseur à la colonne 0.
			Choisir la couleur rouge foncé pour le fond.
			Afficher "Jouer".
			Afficher "\n".
			Choisir la couleur noir pour le fond.
			Afficher "Quitter".
			
			//On met à jour l'option courante choisie
			Copier le résultat de (#ligneOptionCourante - 1) dans #ligneOptionCourante.
		
		//L'option quitter est choisie et on est pas déjà dessus
		sinon si #touchePressee est égale à "DownArrow" et #ligneOptionCourante est différente de #ligneDerniereOption alors
			
			Placer le curseur à la ligne #lignePremiereOption.
			Placer le curseur à la colonne 0.
			Choisir la couleur noir pour le fond.
			Afficher "Jouer".
			Afficher "\n".
			Choisir la couleur rouge foncé pour le fond.
			Afficher "Quitter".
			
			//On met à jour l'option courante choisie
			Copier le résultat de (#ligneOptionCourante + 1) dans #ligneOptionCourante.
		?
		
	>>
	
	//Si on est là c'est que la touche Enter a été pressée
	//On va donc regarder l'option choisie
	
	//Option 'Jouer'
	Si #ligneOptionCourante est égale à #lignePremiereOption alors
		Choisir la couleur noir pour le fond.
		Effacer l'écran.
		
		Créer une zone mémoire nommée #colonneLimiteGauche avec la valeur 0.
		Créer une zone mémoire nommée #colonneLimiteDroite avec la valeur 15.
		Créer une zone mémoire nommée #collecteurX avec la valeur 7.
		Créer une zone mémoire nommée #collecteurY avec la valeur 11.
		
		Créer une zone mémoire nommée #ligneLimite avec la valeur 10.
		Créer une zone mémoire nommée #goutteX avec la valeur 0.
		Créer une zone mémoire nommée #goutteY avec #ligneLimite.
		
		Répéter tant que #touchePressee est différente de "Escape"
			Si ##touche.disponible alors
				Copier ##touche dans #touchePressee.
				
				//Réagir à la touche (faire bouger le bac à pluie)
				Si #touchePressee est égale à "LeftArrow" et #collecteurX est différent de #colonneLimiteGauche alors
					//On efface l'ancienne position
					Placer le curseur à la ligne #collecteurY.
					Placer le curseur à la ligne #collecteurX.
					Afficher " ".
					
					//Calcule la nouvelle position
					Copier le résultat de (#collecteurX - 1) dans #collecteurX.
					
					//On affiche la nouvelle position
					Placer le curseur à la ligne #collecteurY.
					Placer le curseur à la ligne #collecteurX.
					Afficher "U".
					
				sinon si #touchePressee est égale à "RightArrow" et #collecteurX est plus petit que #colonneLimiteDroite alors
					//On efface l'ancienne position
					Placer le curseur à la ligne #collecteurY.
					Placer le curseur à la ligne #collecteurX.
					Afficher " ".
					
					//Calcule la nouvelle position
					Copier le résultat de (#collecteurX + 1) dans #collecteurX.
					
					//On affiche la nouvelle position
					Placer le curseur à la ligne #collecteurY.
					Placer le curseur à la ligne #collecteurX.
					Afficher "U".
				?
			?
			
			//Simuler la pluie
			
			//Efface l'ancienne position de la goutte
			Placer le curseur à la ligne #goutteY.
			Placer le curseur à la colonne #goutteX.
			Afficher " ".
			
			//La goutte est arrivé en bas, on en regénère une
			Si #goutteY est égale à #ligneLimite alors
				Copier 0 dans #goutteY .
				Placer un nombre aléatoire compris entre 1 et 15 dans la zone mémoire nommée #goutteX.
			et sinon
				//On fait descendre la goutte
				Copier le résultat de (#goutteY  + 1) dans #goutteY.
			?
			
			//Dessine la goutte à la nouvelle position
			Placer le curseur à la ligne #goutteY.
			Placer le curseur à la colonne #goutteX.
			Afficher "|".
			
			
			//Vitesse du jeu (20 fps ici)
			Attendre 50ms.
		>>
	?
	
	Effacer l'écran.
	Choisir la couleur noir pour le fond.
	Afficher "\n\nAu revoir !".
	
	
	
Fin de la transmission.

